defmodule AshStorage.Changes.HandleFileArgument do
  @moduledoc false
  use Ash.Resource.Change

  require Ash.Query

  alias AshStorage.Changes.PurgeFilesAfterTransaction
  alias AshStorage.Info
  alias AshStorage.Service.Context

  @impl true
  def init(opts), do: {:ok, opts}

  # sobelow_skip ["DOS.BinToAtom", "Traversal.FileModule"]
  @impl true
  def change(changeset, opts, context) do
    argument_name = opts[:argument]
    attachment_name = opts[:attachment]
    context_opts = Ash.Context.to_opts(context)

    file = Ash.Changeset.get_argument(changeset, argument_name)

    if is_nil(file) do
      changeset
    else
      rollback_ref = make_ref()

      changeset
      |> Ash.Changeset.around_transaction(
        &PurgeFilesAfterTransaction.with_rollback_tracking(&1, &2, rollback_ref),
        prepend?: true
      )
      |> Ash.Changeset.before_action(fn changeset ->
        resource = changeset.resource
        context_opts = Keyword.put(context_opts, :tenant, changeset.tenant)

        case upload_blob(resource, attachment_name, file, changeset, context_opts) do
          {:ok, attrs_to_write, context} ->
            PurgeFilesAfterTransaction.track_rollback(rollback_ref, context.uploaded_file)

            changeset
            |> Ash.Changeset.force_change_attributes(attrs_to_write)
            |> Ash.Changeset.put_context(
              :"__ash_storage_file_arg_#{argument_name}__",
              context
            )

          {:error, error} ->
            Ash.Changeset.add_error(changeset, error)
        end
      end)
      |> Ash.Changeset.after_action(fn changeset, record ->
        context_key = :"__ash_storage_file_arg_#{argument_name}__"

        case changeset.context[context_key] do
          %{blob: blob, attachment_def: attachment_def} = context ->
            context_opts = Keyword.put(context_opts, :tenant, changeset.tenant)

            # On update, replace existing attachment; on create, skip
            with {:ok, keys_to_purge} <-
                   maybe_replace_on_update(
                     changeset,
                     record,
                     attachment_def,
                     context_opts
                   ),
                 {:ok, attachment} <-
                   create_attachment(record, attachment_def, blob, context_opts) do
              # Now that we have the record ID, update write_target for oban analyzers
              if context[:has_oban_analyzers?] do
                update_oban_write_targets(blob, record, context_opts)
                AshOban.run_trigger(blob, :run_pending_analyzers, tenant: changeset.tenant)
              end

              record =
                record
                |> Ash.Resource.put_metadata(:"#{attachment_name}_blob", blob)
                |> Ash.Resource.put_metadata(:"#{attachment_name}_attachment", attachment)
                |> PurgeFilesAfterTransaction.put_record(keys_to_purge)

              {:ok, record}
            end

          _ ->
            {:ok, record}
        end
      end)
      |> Ash.Changeset.after_transaction(
        fn _changeset, result ->
          PurgeFilesAfterTransaction.cleanup_rollback(rollback_ref, result)
        end,
        prepend?: true
      )
      |> Ash.Changeset.after_transaction(&PurgeFilesAfterTransaction.run/2)
    end
  end

  defp maybe_replace_on_update(
         %{action_type: :create},
         _record,
         _attachment_def,
         _context_opts
       ) do
    {:ok, []}
  end

  defp maybe_replace_on_update(_changeset, record, attachment_def, context_opts) do
    maybe_replace_existing(record, attachment_def, context_opts)
  end

  defp upload_blob(resource, attachment_name, file, changeset, context_opts) do
    {filename, content_type} = extract_file_metadata(file)

    with {:ok, attachment_def} <- Info.attachment(resource, attachment_name),
         {:ok, {service_mod, service_opts}} <- resolve_service(resource, attachment_def) do
      ctx = build_context(service_opts, resource, attachment_def, changeset)
      key = AshStorage.resolve_key(attachment_def, ctx, changeset)

      case upload_and_create_blob(resource, service_mod, ctx, file, context_opts,
             key: key,
             filename: filename,
             content_type: content_type
           ) do
        {:ok, blob, upload_ctx} ->
          uploaded_file = {service_mod, upload_ctx, blob.key}

          case run_analyzers(blob, attachment_def, changeset.data, file, context_opts) do
            {:ok, blob, attrs_to_write} ->
              {:ok, attrs_to_write,
               %{
                 blob: blob,
                 attachment_def: attachment_def,
                 uploaded_file: uploaded_file,
                 has_oban_analyzers?: has_oban_analyzers?(attachment_def)
               }}

            {:error, error} ->
              cleanup_failed_upload(error, uploaded_file)
          end

        {:error, error, uploaded_file} ->
          cleanup_failed_upload(error, uploaded_file)

        {:error, error} ->
          {:error, error}
      end
    end
  end

  # After create, we have the real record ID — update write_target in blob's analyzers map
  defp update_oban_write_targets(blob, record, context_opts) do
    analyzers = blob.analyzers || %{}

    has_write_targets? =
      Enum.any?(analyzers, fn {_key, entry} -> Map.has_key?(entry, "write_target") end)

    if has_write_targets? do
      updated =
        Map.new(analyzers, fn {key, entry} ->
          case entry do
            %{"write_target" => _} ->
              {key,
               Map.put(
                 entry,
                 "write_target",
                 %{
                   "resource" => to_string(record.__struct__),
                   "id" => to_string(Map.get(record, :id))
                 }
                 |> maybe_put_tenant(context_opts[:tenant])
               )}

            _ ->
              {key, entry}
          end
        end)

      Ash.update(
        blob,
        %{analyzers: updated},
        Keyword.merge(context_opts, action: :update_metadata)
      )
    end
  end

  defp extract_file_metadata(%Ash.Type.File{} = file) do
    {file_filename(file), file_content_type(file)}
  end

  defp extract_file_metadata(_) do
    {"upload", "application/octet-stream"}
  end

  defp file_filename(%Ash.Type.File{} = file) do
    if function_exported?(Ash.Type.File, :filename, 1) do
      case Ash.Type.File.filename(file) do
        {:ok, name} -> name
        _ -> "upload"
      end
    else
      "upload"
    end
  end

  defp file_content_type(%Ash.Type.File{} = file) do
    if function_exported?(Ash.Type.File, :content_type, 1) do
      case Ash.Type.File.content_type(file) do
        {:ok, type} -> type
        _ -> "application/octet-stream"
      end
    else
      "application/octet-stream"
    end
  end

  # -- Shared helpers (same as Changes.Attach) --

  defp has_oban_analyzers?(attachment_def) do
    Enum.any?(attachment_def.analyzers || [], fn defn -> defn.analyze == :oban end)
  end

  defp run_analyzers(blob, attachment_def, record, io, context_opts) do
    analyzer_defs = attachment_def.analyzers || []

    if analyzer_defs == [] do
      {:ok, blob, %{}}
    else
      normalized = AshStorage.AttachmentDefinition.normalize_analyzers(analyzer_defs)
      content_type = blob.content_type || "application/octet-stream"

      initial_analyzers =
        Map.new(normalized, fn {module, _analyze, opts, write_attributes} ->
          string_opts = Map.new(opts, fn {k, v} -> {to_string(k), v} end)

          entry =
            %{"status" => "pending", "opts" => string_opts}
            |> maybe_put_tenant(context_opts[:tenant])

          entry =
            if write_attributes != [] do
              string_wa =
                Map.new(write_attributes, fn {k, v} -> {to_string(k), to_string(v)} end)

              entry
              |> Map.put("write_attributes", string_wa)
              |> Map.put(
                "write_target",
                %{
                  "resource" => to_string(record.__struct__),
                  "id" => to_string(Map.get(record, :id))
                }
                |> maybe_put_tenant(context_opts[:tenant])
              )
            else
              entry
            end

          {to_string(module), entry}
        end)

      with {:ok, blob} <-
             Ash.update(
               blob,
               %{analyzers: initial_analyzers},
               Keyword.merge(context_opts, action: :update_metadata)
             ) do
        eager =
          Enum.filter(normalized, fn {module, analyze, _opts, _wa} ->
            analyze != :oban && module.accept?(content_type)
          end)

        run_eager_analyzers(blob, eager, io, context_opts)
      end
    end
  end

  defp run_eager_analyzers(blob, [], _io, _context_opts), do: {:ok, blob, %{}}

  defp run_eager_analyzers(blob, eager_analyzers, io, context_opts) do
    {:ok, path} = resolve_analyzer_path(io)

    try do
      Enum.reduce_while(eager_analyzers, {:ok, blob, %{}}, fn {module, _analyze, opts,
                                                               write_attributes},
                                                              {:ok, blob, acc_writes} ->
        analyzer_key = to_string(module)

        {status, metadata_to_merge} =
          case module.analyze(path, opts) do
            {:ok, result} -> {"complete", result}
            {:error, _reason} -> {"error", %{}}
          end

        new_writes =
          if status == "complete" && write_attributes != [] do
            Enum.reduce(write_attributes, %{}, fn {result_key, attr_name}, acc ->
              case Map.fetch(metadata_to_merge, to_string(result_key)) do
                {:ok, value} -> Map.put(acc, attr_name, value)
                :error -> acc
              end
            end)
          else
            %{}
          end

        case Ash.update(
               blob,
               %{
                 analyzer_key: analyzer_key,
                 status: status,
                 metadata_to_merge: metadata_to_merge
               },
               Keyword.merge(context_opts, action: :complete_analysis)
             ) do
          {:ok, blob} -> {:cont, {:ok, blob, Map.merge(acc_writes, new_writes)}}
          {:error, error} -> {:halt, {:error, error}}
        end
      end)
    after
      maybe_cleanup_tempfile(io, path)
    end
  end

  defp maybe_put_tenant(map, nil), do: map
  defp maybe_put_tenant(map, tenant), do: Map.put(map, "tenant", tenant)

  defp resolve_service(resource, attachment_def) do
    case Info.service_for_attachment(resource, attachment_def) do
      {:ok, service} -> {:ok, service}
      :error -> {:error, :no_service_configured}
    end
  end

  defp build_context(service_opts, resource, attachment_def, changeset) do
    Context.new(service_opts,
      resource: resource,
      attachment: attachment_def,
      actor: changeset.context[:private][:actor],
      tenant: changeset.tenant
    )
  end

  defp persistable_service_opts(service_mod, service_opts) do
    if function_exported?(service_mod, :service_opts_fields, 0) do
      fields = service_mod.service_opts_fields()
      field_names = Keyword.keys(fields)

      service_opts
      |> Keyword.take(field_names)
      |> Map.new()
    else
      %{}
    end
  end

  defp upload_and_create_blob(resource, service_mod, ctx, io, context_opts, opts) do
    filename = Keyword.fetch!(opts, :filename)
    content_type = Keyword.get(opts, :content_type, "application/octet-stream")

    data = read_io(io)
    key = Keyword.fetch!(opts, :key)
    checksum = :crypto.hash(:md5, data) |> Base.encode64()
    byte_size = byte_size(data)

    # See `AshStorage.Changes.Attach.upload_and_create_blob/6` for the
    # rationale on threading the blob's content_type / filename into the
    # context before calling `service_mod.upload/3`.
    ctx =
      ctx
      |> Context.put_expected_md5(checksum)
      |> Context.put_blob_metadata(content_type: content_type, filename: filename)

    with {:ok, extra_blob_attrs} <- normalize_upload(service_mod.upload(key, data, ctx)) do
      blob_resource = Info.storage_blob_resource!(resource)

      blob_attrs =
        %{
          key: key,
          filename: filename,
          content_type: content_type,
          byte_size: byte_size,
          checksum: checksum,
          service_name: service_mod,
          service_opts: persistable_service_opts(service_mod, ctx.service_opts),
          metadata: %{}
        }
        |> Map.merge(extra_blob_attrs)

      uploaded_file = {service_mod, ctx, key}

      case Ash.create(blob_resource, blob_attrs, Keyword.merge(context_opts, action: :create)) do
        {:ok, blob} -> {:ok, blob, ctx}
        {:error, error} -> {:error, error, uploaded_file}
      end
    end
  end

  defp cleanup_failed_upload(error, uploaded_file) do
    case PurgeFilesAfterTransaction.purge_now([uploaded_file]) do
      [] -> {:error, error}
      failures -> {:error, {error, {:upload_cleanup_failed, failures}}}
    end
  end

  defp normalize_upload(:ok), do: {:ok, %{}}
  defp normalize_upload({:ok, attrs}) when is_map(attrs), do: {:ok, attrs}
  defp normalize_upload({:error, _} = error), do: error

  defp read_io(%Ash.Type.File{} = file) do
    {:ok, device} = Ash.Type.File.open(file, [:read, :binary])
    data = IO.binread(device, :eof)
    File.close(device)
    data
  end

  defp read_io(%File.Stream{} = stream), do: Enum.into(stream, <<>>, &IO.iodata_to_binary/1)

  # See `AshStorage.Changes.Attach.read_io/1` for the rationale on this
  # clause and the full input contract.
  defp read_io(%{__struct__: Plug.Upload, path: path}) when is_binary(path),
    do: File.read!(path)

  defp read_io(data) when is_binary(data), do: data
  defp read_io(data) when is_list(data), do: IO.iodata_to_binary(data)

  defp resolve_analyzer_path(%Ash.Type.File{} = file) do
    case Ash.Type.File.path(file) do
      {:ok, path} -> {:ok, path}
      _ -> write_tempfile(file)
    end
  end

  defp resolve_analyzer_path(%File.Stream{path: path}), do: {:ok, path}

  defp resolve_analyzer_path(data) when is_binary(data) or is_list(data) do
    write_tempfile(data)
  end

  defp write_tempfile(%Ash.Type.File{} = file) do
    {:ok, device} = Ash.Type.File.open(file, [:read, :binary])
    data = IO.binread(device, :eof)
    File.close(device)
    write_tempfile(data)
  end

  # sobelow_skip ["Traversal.FileModule"]
  defp write_tempfile(data) when is_binary(data) do
    path = Path.join(System.tmp_dir!(), "ash_storage_analyze_#{AshStorage.generate_key()}")
    File.write!(path, data)
    {:ok, path}
  end

  defp write_tempfile(data) when is_list(data), do: write_tempfile(IO.iodata_to_binary(data))

  # sobelow_skip ["Traversal.FileModule"]
  defp maybe_cleanup_tempfile(%Ash.Type.File{} = file, path) do
    case Ash.Type.File.path(file) do
      {:ok, ^path} -> :ok
      _ -> File.rm(path)
    end
  end

  defp maybe_cleanup_tempfile(%File.Stream{}, _path), do: :ok
  # sobelow_skip ["Traversal.FileModule"]
  defp maybe_cleanup_tempfile(_data, path), do: File.rm(path)

  # sobelow_skip ["DOS.BinToAtom"]
  defp maybe_replace_existing(record, %{type: :one} = attachment_def, context_opts) do
    case find_attachments(record, attachment_def, context_opts) do
      {:ok, []} -> {:ok, []}
      {:ok, existing} -> purge_attachments(existing, record, attachment_def, context_opts)
      {:error, _} = error -> error
    end
  end

  defp maybe_replace_existing(_record, %{type: :many}, _context_opts), do: {:ok, []}

  # sobelow_skip ["DOS.BinToAtom"]
  defp create_attachment(record, attachment_def, blob, context_opts) do
    resource = record.__struct__
    attachment_resource = Info.storage_attachment_resource!(resource)
    record_id = Map.get(record, :id) |> to_string()

    belongs_to_resources =
      Spark.Dsl.Extension.get_entities(attachment_resource, [:attachment])

    parent_rel = Enum.find(belongs_to_resources, fn bt -> bt.resource == resource end)

    params =
      if parent_rel do
        fk_attr = :"#{parent_rel.name}_id"

        Map.new([
          {:name, to_string(attachment_def.name)},
          {fk_attr, record_id},
          {:blob_id, blob.id}
        ])
      else
        %{
          name: to_string(attachment_def.name),
          record_type: to_string(resource),
          record_id: record_id,
          blob_id: blob.id
        }
      end

    Ash.create(attachment_resource, params, Keyword.merge(context_opts, action: :create))
  end

  # sobelow_skip ["DOS.BinToAtom"]
  defp find_attachments(record, attachment_def, context_opts) do
    resource = record.__struct__
    attachment_resource = Info.storage_attachment_resource!(resource)
    record_id = Map.get(record, :id) |> to_string()

    belongs_to_resources =
      Spark.Dsl.Extension.get_entities(attachment_resource, [:attachment])

    parent_rel = Enum.find(belongs_to_resources, fn bt -> bt.resource == resource end)

    filter =
      if parent_rel do
        [{:name, to_string(attachment_def.name)}, {:"#{parent_rel.name}_id", record_id}]
      else
        [
          name: to_string(attachment_def.name),
          record_type: to_string(resource),
          record_id: record_id
        ]
      end

    attachment_resource
    |> Ash.Query.filter(^filter)
    |> Ash.Query.load(:blob)
    |> Ash.read(Keyword.take(context_opts, [:actor, :tenant, :authorize?, :tracer]))
  end

  defp purge_attachments(attachments, record, attachment_def, context_opts) do
    destroy_opts = Keyword.merge(context_opts, action: :destroy, return_destroyed?: true)

    Enum.reduce_while(attachments, {:ok, []}, fn att, {:ok, keys_acc} ->
      blob = att.blob
      service_mod = blob.service_name
      loaded_blob = Ash.load!(blob, :parsed_service_opts, context_opts)

      ctx =
        Context.new(loaded_blob.parsed_service_opts || [],
          resource: record.__struct__,
          attachment: attachment_def,
          actor: context_opts[:actor],
          tenant: context_opts[:tenant]
        )

      with {:ok, _} <- Ash.destroy(att, destroy_opts),
           {:ok, _} <- Ash.destroy(blob, destroy_opts) do
        {:cont, {:ok, [{service_mod, ctx, blob.key} | keys_acc]}}
      else
        {:error, error} -> {:halt, {:error, error}}
      end
    end)
  end
end
